grammar Pinn;



file : ( function | statement )+ EOF ;

function
  : 'func' ID LPAREN (fvarDecl (',' fvarDecl)*)?  ')' kind? block ;

testRule
  : name=expr;

block
  : '{' statement* '}' ;

fvarDecl
  : ID THREEDOT? kind ;

varDecl
  : 'var' ID kind
  | ID CE expr
  | LPAREN ID ( ',' ID )* ')' CE expr;

LSQUARE : '[' ;
LPAREN : '(' ;
kind
  : TYPES
  | (LSQUARE ( MAP | SLICE | expr) ']') kind
  |  LPAREN kindList ')' ;

MAP
 : 'map' ;

SLICE
 : 'slice' ;

TYPES
  : ('int' | 'bool' |'string' | 'decimal' | 'self' ) ;

simpleStatement
  : lExpr '=' expr #simpleSet
  | lExpr ('+' | '-' | '^' | BINOP) '=' expr #compoundSet
  | lExpr DOUBLEOP #doubleSet ;

lExpr
  : ID (LSQUARE expr ']')* ;

objectPair :
STRING ':' expr ;

expr
  :
   expr LSQUARE ( first=expr? (TWODOTS | COLON) second=expr? | expr) ']' #indexExpr
  |   LSQUARE exprList ']' #arrayLiteral
  | '{' objectPair ( ',' objectPair )* '}' #objectLiteral
  |    ('+' | '-' | '!' ) expr #unaryExpr
  | expr ('+' | '-' | BINOP) expr #intExpr
  | expr ('==' | '!=' | '>' | '<' | '>=' | '<=' ) expr #compExpr
  | expr ('&&' | '||') expr #boolExpr
  |   ID LPAREN exprList? ')' #callExpr
  | AST? LPAREN expr ')' #parenExpr
  |  '@'? LPAREN exprList ')' #tupleExpr
  | expr (TWODOTS | COLON) expr #rangeExpr
  | expr '?' expr COLON expr #conditionalExpr
  | (ID | FLOAT | INT | BOOL | STRING | NIL ) #literalExpr ;
  
exprList
  : expr (',' expr)* ;
kindList
  : kind (',' kind)* ;

returnStatement
  : 'return' expr? ;

ifStatement
  : 'if' expr statement ('else' statement)?;

guardStatement
  : 'guard' expr 'else' block;

whStatement
  : 'while' expr block ;
  
loopStatement
  : 'loop' block ;

repeatStatement
  : 'repeat' block 'while' expr ;


foStatement
  : 'for' (varDecl | fss=simpleStatement)? ';' expr ';' sss=simpleStatement block
  | 'for' ID COMMA ID '=' RANGE expr block |
    'for' ID '=' RANGE expr block ;
caseStatement
  : 'when' exprList COLON statement* ;

switchStatement
  : 'match' expr '{' caseStatement* ('default' COLON statement*)? '}' ;

statement
  :  expr ';'
  | varDecl ';' 
  | simpleStatement ';'
  | ifStatement
  | guardStatement
  | whStatement 
  | repeatStatement ';'
  | loopStatement
  | switchStatement
  | returnStatement ';'
  | foStatement
  | block
  | 'break' ';'
  | 'continue' ';'
  | 'fallthrough' ';'
  | ';' ;

NIL : 'nil' ;
COMMA : ',' ;
COLON : ':' ;
CE : ':=' ;
IOTA : 'iota' ;
BINOP : (AST | '/' | '%' ) ;

DOUBLEOP
: '++' | '--' ;

RANGE : 'range' ;

BOOL : 'true' | 'false' ;
AST: '*' ;
THREEDOT
: '...' ;
TWODOTS : '@' ;

ID : [a-zA-Z_]([a-zA-Z_0-9])* ;
CHAR : '\''[a-zA-Z_0-9]'\'' ;
INT : '0'
  | [1-9] ('_'? DECIMAL_DIGITS)?
  | '0x' '_'? HEX_DIGIT ('_'? HEX_DIGIT)*
  | '0b' '_'? BINARY_DIGIT ('_'? BINARY_DIGIT)*
  | '0o' '_'? OCTAL_DIGIT ('_'? OCTAL_DIGIT)* ;

FLOAT : DECIMAL_DIGITS '.' DECIMAL_DIGITS? DECIMAL_EXPONENT?
  | DECIMAL_DIGITS DECIMAL_EXPONENT
  | '.' DECIMAL_DIGITS DECIMAL_EXPONENT?
  | '0x' HEX_MANTISSA HEX_EXPONENT ;

WS : ([ \t\n]+ | '//' ~('\n')* '\n' | '/*' .*? '*/' )-> skip ;
STRING : '"' ( '\\"' | ~('"' | '\\') )*      '"' ;

fragment DECIMAL_DIGIT : [0-9] ;
fragment DECIMAL_DIGITS : DECIMAL_DIGIT ('_'? DECIMAL_DIGIT)* ;
fragment DECIMAL_EXPONENT : 'e' [+-]? DECIMAL_DIGITS ;
fragment HEX_DIGIT : [0-9a-f] ;
fragment HEX_DIGITS: HEX_DIGIT ('_'? HEX_DIGIT)* ;
fragment HEX_MANTISSA : '_'? HEX_DIGITS '.' HEX_DIGITS?
  | '_'? HEX_DIGITS
  | '.' HEX_DIGITS ;
fragment HEX_EXPONENT : 'p' [+-]? DECIMAL_DIGITS 
  | 'h' [+-]? HEX_DIGITS ;
fragment OCTAL_DIGIT : [0-7] ;
fragment BINARY_DIGIT : [01] ;

